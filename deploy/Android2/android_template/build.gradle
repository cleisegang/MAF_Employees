// Top-level build file where you can add configuration options common to all sub-projects/modules.

// read in any properties that are securely sent from the command-line
if (project.hasProperty('maf_secure_props'))
{
    def secureProps = maf_secure_props.split(',')
    
    def lines = System.in.readLines()
    
    if (secureProps.size() != lines.size())
    {
        throw new GradleException("Input defined ${secureProps.size()} properties, but received ${lines.size()}.")
    }
    def index = 0;
    for (prop in secureProps)
    {
        project.ext[prop] = lines[index++]
    }
}

if (!project.hasProperty('maf_asset_packaging'))
{
  project.ext['maf_asset_packaging'] = 'rt'
}
else
{
  def packaging = project.ext.maf_asset_packaging

  if (packaging != 'rt' && packaging != 'none')
  {
    throw new GradleException("Invalid packaging type defined: $packaging.")
  }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
